
******************************************* EXAMPLE SETUP ************************************************

# IMPORT PROVONE TRACE

blazegraph destroy --dataset kb
blazegraph create --dataset kb --quiet --infer owl
blazegraph import --file ../data/wt-prov-rules.ttl
blazegraph import --format jsonld --file ../data/branched-pipeline.jsonld

------------------------------------------- SETUP OUTPUTS ------------------------------------------------



******************************************* EXAMPLE RETROSPECTIVE-1 ************************************************

# WHAT DATA FILES WERE USED AS INPUT BY THE TALE?

blazegraph query --format table << __END_QUERY__

    PREFIX prov: <http://www.w3.org/ns/prov#>
    PREFIX provone: <http://purl.dataone.org/provone/2015/01/15/ontology#>
    PREFIX wt: <http://wholetale.org/ontology/wt/>

    SELECT DISTINCT ?run ?tale_input_file_path ?read_file
    WHERE {
        ?run rdf:type wt:TaleRun .                              # Identify the Tale run described by this JSON-LD document.
        ?run wt:TaleRunScript ?run_script .                     # Identify the script used to run the Tale as a whole.
        ?run_process wt:ExecutionOf ?run_script .               # Identify the process that is the execution of that run script.
        ?run_sub_process (wt:ChildProcessOf)+ ?run_process .    # Find all child processes of the run script execution.
        ?run_sub_process wt:ReadFile ?read_file .               # Identify files read by those run subprocesses.
        FILTER NOT EXISTS {                                     # Filter out any files written by the Tale run, leaving
            ?tale_process wt:WroteFile ?read_file . }           #   just the input files.
        ?read_file wt:FilePath ?tale_input_file_path .          # Get the file path for each of the input files.
    }
    ORDER BY ?tale_input_file_path

__END_QUERY__

------------------------------------------- RETROSPECTIVE-1 OUTPUTS ------------------------------------------------

run                                                   | tale_input_file_path | read_file
===========================================================================================================================================
http://wholetale.org/tale/2035/version/1021/run/1119/ | ./inputs/i1.txt      | http://wholetale.org/tale/2035/version/1021/run/1119/datafile/23
http://wholetale.org/tale/2035/version/1021/run/1119/ | ./inputs/i2.txt      | http://wholetale.org/tale/2035/version/1021/run/1119/datafile/24
http://wholetale.org/tale/2035/version/1021/run/1119/ | ./inputs/i3.txt      | http://wholetale.org/tale/2035/version/1021/run/1119/datafile/26
http://wholetale.org/tale/2035/version/1021/run/1119/ | ./inputs/i4.txt      | http://wholetale.org/tale/2035/version/1021/run/1119/datafile/28


******************************************* EXAMPLE RETROSPECTIVE-2 ************************************************

# WHAT FILES WERE PRODUCED AS OUTPUTS OF THE TALE?

blazegraph query --format table << __END_QUERY__

    PREFIX prov: <http://www.w3.org/ns/prov#>
    PREFIX provone: <http://purl.dataone.org/provone/2015/01/15/ontology#>
    PREFIX wt: <http://wholetale.org/ontology/wt/>

    SELECT DISTINCT ?tale_output_file_path ?written_file
    WHERE {
        ?run rdf:type wt:TaleRun .                          # Identify the Tale run described by this JSON-LD document.
        ?run wt:TaleRunScript ?run_script .                 # Identify the script used to run the Tale as a whole.
        ?run_process wt:ExecutionOf ?run_script .           # Identify the process that is the execution of that run script.
        ?run_sub_process (wt:ChildProcessOf)+  ?run_process .    # Find all child processes of the run script execution.
        ?run_sub_process wt:WroteFile ?written_file .       # Identify files written by those run subprocesses.
        FILTER NOT EXISTS { ?written_file                   # Filter out intermediate products of the Tale run, leaving
            wt:FileRole wt:TaleIntermediateData . }         #   just the finial output input files.
        ?written_file wt:FilePath ?tale_output_file_path .  # Get the file path for each of the output files.
    }

__END_QUERY__

------------------------------------------- RETROSPECTIVE-2 OUTPUTS ------------------------------------------------

tale_output_file_path | written_file
======================================================================================
./outputs/o12.txt     | http://wholetale.org/tale/2035/version/1021/run/1119/datafile/29
./outputs/o1234.txt   | http://wholetale.org/tale/2035/version/1021/run/1119/datafile/30
./outputs/o4.txt      | http://wholetale.org/tale/2035/version/1021/run/1119/datafile/31

