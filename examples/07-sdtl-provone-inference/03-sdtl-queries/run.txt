
******************************************* EXAMPLE SETUP ************************************************

# IMPORT SDTL

blazegraph destroy --dataset kb
blazegraph create --dataset kb
blazegraph import --format jsonld --file ../data/compute-sdtl.jsonld

------------------------------------------- SETUP OUTPUTS ------------------------------------------------



******************************************* EXAMPLE Q1 ************************************************

# WHAT COMMANDS ARE EXECUTED BY THE SCRIPT?

blazegraph select --format table << __END_QUERY__

    PREFIX sdtl: <https://rdf-vocabulary.ddialliance.org/sdtl#>

    SELECT DISTINCT ?command ?sourceline ?sourcetext
    WHERE {
        ?program rdf:type sdtl:Program .
        ?program sdtl:Commands ?command .
        ?command sdtl:SourceInformation ?sourceinfo .
        ?sourceinfo sdtl:LineNumberStart ?sourceline .
        ?sourceinfo sdtl:OriginalSourceText ?sourcetext .
    } ORDER BY ?line

__END_QUERY__

------------------------------------------- Q1 OUTPUTS ------------------------------------------------

command    | sourceline | sourcetext
==================================================================================
:load/1    | 4          | df = pd.read_csv('../data/df.csv')
:compute/1 | 8          | df['A'] = 3
:compute/2 | 12         | df['B'] = df.B + 6.5
:compute/3 | 16         | df['C'] = df.A - df.B
:load/2    | 20         | temps = pd.read_csv('../data/temps.csv')
:compute/4 | 24         | temps = temps.assign(Celsius=((temps.Fahrenheit - 32) * 5 / 9))
:compute/5 | 28         | temps = temps.assign(Kelvin=(temps.Celsius + 273))
:save/1    | 32         | df.to_csv('outputs/df_updated.csv', index=False)
:save/2    | 35         | temps.to_csv('outputs/temps_updated.csv', index=False)


******************************************* EXAMPLE Q2 ************************************************

# WHAT DATA FILES ARE LOADED BY THE SCRIPT?

blazegraph select --format table << __END_QUERY__

    PREFIX sdtl: <https://rdf-vocabulary.ddialliance.org/sdtl#>

    SELECT DISTINCT ?filename ?command ?sourceline ?sourcetext
    WHERE {
        ?program rdf:type sdtl:Program .
        ?program sdtl:Commands ?command .
        ?command rdf:type sdtl:Load .
        ?command sdtl:FileName ?filename .
        ?command sdtl:SourceInformation ?sourceinfo .
        ?sourceinfo sdtl:LineNumberStart ?sourceline .
        ?sourceinfo sdtl:OriginalSourceText ?sourcetext .
    }

__END_QUERY__

------------------------------------------- Q2 OUTPUTS ------------------------------------------------

filename  | command | sourceline | sourcetext
=====================================================================
df.csv    | :load/1 | 4          | df = pd.read_csv('../data/df.csv')
temps.csv | :load/2 | 20         | temps = pd.read_csv('../data/temps.csv')


******************************************* EXAMPLE Q3 ************************************************

# WHAT DATA FILES ARE SAVED BY THE SCRIPT?

blazegraph select --format table << __END_QUERY__

    PREFIX sdtl: <https://rdf-vocabulary.ddialliance.org/sdtl#>

    SELECT DISTINCT ?filename ?command ?sourceline ?sourcetext
    WHERE {
        ?program rdf:type sdtl:Program .
        ?program sdtl:Commands ?command .
        ?command rdf:type sdtl:Save .
        ?command sdtl:FileName ?filename .
        ?command sdtl:SourceInformation ?sourceinfo .
        ?sourceinfo sdtl:LineNumberStart ?sourceline .
        ?sourceinfo sdtl:OriginalSourceText ?sourcetext .
    }

__END_QUERY__

------------------------------------------- Q3 OUTPUTS ------------------------------------------------

filename          | command | sourceline | sourcetext
===========================================================================================
df_updated.csv    | :save/1 | 32         | df.to_csv('outputs/df_updated.csv', index=False)
temps_updated.csv | :save/2 | 35         | temps.to_csv('outputs/temps_updated.csv', index=False)


******************************************* EXAMPLE Q4 ************************************************

# WHAT VARIABLES ARE LOADED BY THE SCRIPT?

blazegraph select --format table << __END_QUERY__

    PREFIX sdtl: <https://rdf-vocabulary.ddialliance.org/sdtl#>

    SELECT DISTINCT ?variablename ?dataframe ?sourceline ?sourcetext
    WHERE {
        ?program rdf:type sdtl:Program .
        ?program sdtl:Commands ?command .
        ?command rdf:type sdtl:Load .
        ?command sdtl:ProducesDataframe ?dataframedesc .
        ?dataframedesc sdtl:DataframeName ?dataframe .
        ?dataframedesc sdtl:VariableInventory ?variablename .
        ?command sdtl:SourceInformation ?sourceinfo .
        ?sourceinfo sdtl:LineNumberStart ?sourceline .
        ?sourceinfo sdtl:OriginalSourceText ?sourcetext .
    }

__END_QUERY__

------------------------------------------- Q4 OUTPUTS ------------------------------------------------

variablename | dataframe | sourceline | sourcetext
====================================================================
A            | df        | 4          | df = pd.read_csv('../data/df.csv')
B            | df        | 4          | df = pd.read_csv('../data/df.csv')
Fahrenheit   | temps     | 20         | temps = pd.read_csv('../data/temps.csv')


******************************************* EXAMPLE Q5 ************************************************

# WHAT VARIABLES ARE SAVED BY THE SCRIPT?

blazegraph select --format table << __END_QUERY__

    PREFIX sdtl: <https://rdf-vocabulary.ddialliance.org/sdtl#>

    SELECT DISTINCT ?variablename ?dataframe ?sourceline ?sourcetext
    WHERE {
        ?program rdf:type sdtl:Program .
        ?program sdtl:Commands ?command .
        ?command rdf:type sdtl:Save .
        ?command sdtl:ProducesDataframe ?dataframedesc .
        ?dataframedesc sdtl:DataframeName ?dataframe .
        ?dataframedesc sdtl:VariableInventory ?variablename .
        ?command sdtl:SourceInformation ?sourceinfo .
        ?sourceinfo sdtl:LineNumberStart ?sourceline .
        ?sourceinfo sdtl:OriginalSourceText ?sourcetext .
    }

__END_QUERY__

------------------------------------------- Q5 OUTPUTS ------------------------------------------------

variablename | dataframe | sourceline | sourcetext
==================================================================================
A            | df        | 32         | df.to_csv('outputs/df_updated.csv', index=False)
B            | df        | 32         | df.to_csv('outputs/df_updated.csv', index=False)
C            | df        | 32         | df.to_csv('outputs/df_updated.csv', index=False)
Celsius      | temps     | 35         | temps.to_csv('outputs/temps_updated.csv', index=False)
Fahrenheit   | temps     | 35         | temps.to_csv('outputs/temps_updated.csv', index=False)
Kelvin       | temps     | 35         | temps.to_csv('outputs/temps_updated.csv', index=False)


******************************************* EXAMPLE Q6 ************************************************

# WHAT COMMANDS AFFECT EACH DATAFRAME?

blazegraph select --format table << __END_QUERY__

    PREFIX sdtl: <https://rdf-vocabulary.ddialliance.org/sdtl#>

    SELECT DISTINCT ?dataframe ?command ?sourceline ?sourcetext
    WHERE {
        ?program rdf:type sdtl:Program .
        ?program sdtl:Commands ?command .
        ?command sdtl:ProducesDataframe ?dataframedesc .
        ?dataframedesc sdtl:DataframeName ?dataframe .
        ?dataframedesc sdtl:VariableInventory ?variable .
        ?command sdtl:SourceInformation ?sourceinfo .
        ?sourceinfo sdtl:LineNumberStart ?sourceline .
        ?sourceinfo sdtl:OriginalSourceText ?sourcetext .
    } ORDER BY ?dataframe ?sourceline

__END_QUERY__

------------------------------------------- Q6 OUTPUTS ------------------------------------------------

dataframe | command    | sourceline | sourcetext
===========================================================================================
df        | :load/1    | 4          | df = pd.read_csv('../data/df.csv')
df        | :compute/1 | 8          | df['A'] = 3
df        | :compute/2 | 12         | df['B'] = df.B + 6.5
df        | :compute/3 | 16         | df['C'] = df.A - df.B
df        | :save/1    | 32         | df.to_csv('outputs/df_updated.csv', index=False)
temps     | :load/2    | 20         | temps = pd.read_csv('../data/temps.csv')
temps     | :compute/4 | 24         | temps = temps.assign(Celsius=((temps.Fahrenheit - 32) * 5 / 9))
temps     | :compute/5 | 28         | temps = temps.assign(Kelvin=(temps.Celsius + 273))
temps     | :save/2    | 35         | temps.to_csv('outputs/temps_updated.csv', index=False)


******************************************* EXAMPLE Q7 ************************************************

# WHAT COMMANDS AFFECT EACH VARIABLE?

blazegraph select --format table << __END_QUERY__

    PREFIX sdtl: <https://rdf-vocabulary.ddialliance.org/sdtl#>

    SELECT DISTINCT ?variablename ?command ?sourceline ?sourcetext
    WHERE {
        ?program rdf:type sdtl:Program .
        ?program sdtl:Commands ?command .
        ?command sdtl:Variable ?variable .
        ?variable sdtl:VariableName ?variablename .
        ?command sdtl:SourceInformation ?sourceinfo .
        ?sourceinfo sdtl:LineNumberStart ?sourceline .
        ?sourceinfo sdtl:OriginalSourceText ?sourcetext .
    } ORDER BY ?variable ?sourceline

__END_QUERY__

------------------------------------------- Q7 OUTPUTS ------------------------------------------------

variablename | command    | sourceline | sourcetext
=============================================================================================
A            | :compute/1 | 8          | df['A'] = 3
B            | :compute/2 | 12         | df['B'] = df.B + 6.5
C            | :compute/3 | 16         | df['C'] = df.A - df.B
Celsius      | :compute/4 | 24         | temps = temps.assign(Celsius=((temps.Fahrenheit - 32) * 5 / 9))
Kelvin       | :compute/5 | 28         | temps = temps.assign(Kelvin=(temps.Celsius + 273))

