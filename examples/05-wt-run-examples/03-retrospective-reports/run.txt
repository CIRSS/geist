
******************************************* EXAMPLE SETUP ************************************************

# IMPORT PROVONE TRACE

blazegraph drop
blazegraph import --format jsonld --file ../data/branched-pipeline.jsonld

------------------------------------------- SETUP OUTPUTS ------------------------------------------------



******************************************* EXAMPLE REPORT-1 ************************************************

# WHAT DATA FILES WERE USED AS INPUT BY THE TALE?

blazegraph report << __END_REPORT_TEMPLATE__

{{ prefix "prov" "http://www.w3.org/ns/prov#" }}
{{ prefix "provone" "http://purl.dataone.org/provone/2015/01/15/ontology#" }}
{{ prefix "wt" "http://wholetale.org/ontology/wt#" }}

{{ select '''

    SELECT DISTINCT ?tale_input_file_path ?read_file
    WHERE {
        ?run rdf:type wt:TaleRun .                          
        ?run wt:TaleRunScript ?run_script .                 
        ?run_process wt:ExecutionOf ?run_script .               
        ?run_sub_process wt:ChildProcessOf ?run_process .   
        ?run_sub_process wt:ReadFile ?read_file .          
        FILTER NOT EXISTS {                               
            ?_ wt:WroteFile ?read_file . }     
        ?read_file wt:FilePath ?tale_input_file_path .
    }
    ORDER BY ?tale_input_file_path

''' | tabulate }}

__END_REPORT_TEMPLATE__

------------------------------------------- REPORT-1 OUTPUTS ------------------------------------------------

tale_input_file_path | read_file
====================================================================================
./inputs/i1.txt      | http://wholetale.org/tale_2035/version_1021/run_1119#data_file_i23
./inputs/i2.txt      | http://wholetale.org/tale_2035/version_1021/run_1119#data_file_i24
./inputs/i3.txt      | http://wholetale.org/tale_2035/version_1021/run_1119#data_file_i26
./inputs/i4.txt      | http://wholetale.org/tale_2035/version_1021/run_1119#data_file_i28


******************************************* EXAMPLE REPORT-2 ************************************************

# WHAT DATA FILES WERE USED AS INPUT BY THE TALE?

blazegraph report << '__END_REPORT_TEMPLATE__'

{{ prefix "prov" "http://www.w3.org/ns/prov#" }}
{{ prefix "provone" "http://purl.dataone.org/provone/2015/01/15/ontology#" }}
{{ prefix "wt" "http://wholetale.org/ontology/wt#" }}

{{ with $Run := (select "SELECT ?r WHERE {?r a wt:TaleRun}") | value }}

    {{ println "Tale Run:   " $Run }}

    {{ println "Tale Name:  " (select '''
            SELECT ?n WHERE {<{{.}}> wt:TaleName ?n}''' $Run | value) }}

    {{ with $RunScript := (select '''
            SELECT ?s WHERE {<{{.}}> wt:TaleRunScript ?s}''' $Run | value) }}

        {{ println "Tale Script:" (select '''
            SELECT ?n WHERE {<{{.}}> wt:FilePath ?n}''' $RunScript | value) }}

        {{ println }}
        {{ println "Tale Inputs:" }} 
        {{ range $InputFile := (select '''
            SELECT DISTINCT ?fp WHERE {
                ?e wt:ExecutionOf <{{.}}> .               
                ?p wt:ChildProcessOf ?e .   
                ?p wt:ReadFile ?f .          
                FILTER NOT EXISTS {                               
                    ?_ wt:WroteFile ?f . }     
                ?f wt:FilePath ?fp .
            } ORDER BY ?fp''' $RunScript | vector) }}

            {{println $InputFile}}

        {{end}}
    {{end}}
{{end}}

__END_REPORT_TEMPLATE__

------------------------------------------- REPORT-2 OUTPUTS ------------------------------------------------

Tale Run:    http://wholetale.org/tale2035/version_1021/run_1119#
Tale Name:   08-branched-pipeline
Tale Script: ./run.sh

Tale Inputs:
./inputs/i1.txt
./inputs/i2.txt
./inputs/i3.txt
./inputs/i4.txt


******************************************* EXAMPLE REPORT-3 ************************************************

# WHAT DATA FILES WERE USED AS INPUT BY THE TALE?

blazegraph report << '__END_REPORT_TEMPLATE__'

{{{
{{ prefix "prov" "http://www.w3.org/ns/prov#" }}
{{ prefix "provone" "http://purl.dataone.org/provone/2015/01/15/ontology#" }}
{{ prefix "wt" "http://wholetale.org/ontology/wt#" }}

{{ query "GetRunID" '''
    SELECT ?r 
    WHERE {
        ?r a wt:TaleRun
    }
''' }}

{{ query "GetTaleName" '''
    SELECT ?n 
    WHERE {
        <{{.}}> wt:TaleName ?n
    }
''' }}

{{ query "GetRunScriptID" '''
    SELECT ?s 
    WHERE {
        <{{.}}> wt:TaleRunScript ?s
    }
''' }}

{{ query "GetFilePath" '''
    SELECT ?n
    WHERE {
        <{{.}}> wt:FilePath ?n
    }
''' }}

{{ query "GetInputFilePaths" '''
    SELECT DISTINCT ?fp
    WHERE {
        ?e wt:ExecutionOf <{{.}}> .               
        ?p wt:ChildProcessOf ?e .   
        ?p wt:ReadFile ?f .          
        FILTER NOT EXISTS {
            ?_ wt:WroteFile ?f . 
        }
        ?f wt:FilePath ?fp .
    }
    ORDER BY ?fp
''' }}
}}}

{{ with $RunID := GetRunID | value }}
    {{ println "Tale Run:   " $RunID }}
    {{ println "Tale Name:  " (GetTaleName $RunID | value) }}
    {{ with $RunScriptID := (GetRunScriptID $RunID | value) }}
        {{ println "Tale Script:" (GetFilePath $RunScriptID | value) }}
        {{ println }}
        {{ println "Tale Inputs:" }} 
        {{ range (GetInputFilePaths $RunScriptID | vector) }}
            {{ println . }}
        {{ end }}
    {{ end }}
{{ end }}

__END_REPORT_TEMPLATE__

------------------------------------------- REPORT-3 OUTPUTS ------------------------------------------------

Tale Run:    http://wholetale.org/tale2035/version_1021/run_1119#
Tale Name:   08-branched-pipeline
Tale Script: ./run.sh

Tale Inputs:
./inputs/i1.txt
./inputs/i2.txt
./inputs/i3.txt
./inputs/i4.txt

